# Cyclic groups

```agda
module group-theory.cyclic-groups where
```

<details><summary>Imports</summary>

```agda
open import foundation.embeddings
open import foundation.existential-quantification
open import foundation.propositions
open import foundation.universe-levels

open import group-theory.full-subgroups
open import group-theory.groups
open import group-theory.homomorphisms-groups
open import group-theory.subgroups-generated-by-elements-groups
```

</details>

## Idea

A [group](group-theory.groups.md) `G` is said to be **cyclic** if it is
generated by a single element, i.e., if there
[exists](foundation.existential-quantification.md) an element `g : G` such that
the evaluation map

```text
  hom-Group G H → H
```

given by `f ↦ f g` is an [embedding](foundation.embeddings.md) for every group
`H`.

## Definitions

### Cyclic groups

```agda
ev-element-Group :
  {l1 l2 : Level} (G : Group l1) (H : Group l2) (g : type-Group G) →
  type-hom-Group G H → type-Group H
ev-element-Group G H g f = map-hom-Group G H f g

module _
  {l1 : Level} (l2 : Level) (G : Group l1)
  where

  is-cyclic-prop-Group : Prop (l1 ⊔ lsuc l2)
  is-cyclic-prop-Group =
    ∃-Prop
      ( type-Group G)
      ( λ g → (H : Group l2) → is-emb (ev-element-Group G H g))

  is-cyclic-Group : UU (l1 ⊔ lsuc l2)
  is-cyclic-Group = type-Prop is-cyclic-prop-Group

  is-prop-is-cyclic-Group : is-prop is-cyclic-Group
  is-prop-is-cyclic-Group = is-prop-type-Prop is-cyclic-prop-Group
```

### Groups with a generating element

```agda
module _
  {l1 : Level} (G : Group l1)
  where

  is-generating-element-Group : (g : type-Group G) → UU l1
  is-generating-element-Group g =
    is-full-Subgroup G (subgroup-element-Group G g)
```

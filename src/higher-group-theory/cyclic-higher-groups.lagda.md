# Cyclic higher groups

```agda
module higher-group-theory.cyclic-higher-groups where
```

<details><summary>Imports</summary>

```agda
open import foundation.embeddings
open import foundation.existential-quantification
open import foundation.propositions
open import foundation.universe-levels

open import structured-types.pointed-maps
open import structured-types.pointed-types

open import synthetic-homotopy-theory.functoriality-loop-spaces
open import synthetic-homotopy-theory.loop-spaces
```

</details>

## Idea

A [pointed type](structured-types.pointed-types.md) `A` is said to be cyclic if
there [exists](foundation.existential-quantification.md) a
[loop](synthetic-homotopy-theory.loop-spaces.md) `ω : Ω A` such that the
evaluation map

```text
  (A →∗ B) → Ω B
```

given by `f ↦ Ω f ω` is an [embedding](foundation.embeddings.md) for every
pointed type `B`. In other words, a pointed type is cyclic if its loop space is
generated by a single element.

## Definitions

```agda
ev-loop-Pointed-Type :
  {l1 l2 : Level} {A : Pointed-Type l1} {B : Pointed-Type l2} (ω : type-Ω A) →
  (A →∗ B) → type-Ω B
ev-loop-Pointed-Type ω f = map-Ω f ω

module _
  {l1 : Level} (A : Pointed-Type l1)
  where

  is-cyclic-prop-Pointed-Type : (l2 : Level) → Prop (l1 ⊔ lsuc l2)
  is-cyclic-prop-Pointed-Type l2 =
    ∃-Prop
      ( type-Ω A)
      ( λ ω →
        (B : Pointed-Type l2) →
        is-emb (ev-loop-Pointed-Type {l1} {l2} {A} {B} ω))
```
